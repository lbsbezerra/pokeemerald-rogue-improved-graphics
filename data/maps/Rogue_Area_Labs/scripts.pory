const FLAG_HIDE_ELM = FLAG_TEMP_1

const VAR_RETURN_STATE = VAR_TEMP_0

const OBJ_EVENT_ELM = 2

mapscripts Rogue_Area_Labs_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Area_Labs_OnMapLoad
    MAP_SCRIPT_ON_FRAME_TABLE 
    [
        VAR_RETURN_STATE, 0: Rogue_Area_Labs_WarpState0
    ]
}

script Rogue_Area_Labs_OnMapLoad
{
    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_SPAWN)
    {
        clearflag(FLAG_HIDE_ELM)
    }
    elif(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_LEAVE_LAB)
    {
        clearflag(FLAG_HIDE_ELM)
        setobjectxyperm(OBJ_EVENT_ELM, 7, 9)
        setobjectmovementtype(OBJ_EVENT_ELM, MOVEMENT_TYPE_FACE_UP)
    }
    else
    {
        setflag(FLAG_HIDE_ELM)
    }
}

script Rogue_Area_Labs_WarpState0
{
    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_SPAWN)
    {
        call(Rogue_Area_Labs_StartGame)
    }
    elif(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_LEAVE_LAB)
    {
        call(Rogue_Area_Labs_TutorialGuide)
    }
    setvar(VAR_RETURN_STATE, 1)
}

script Rogue_Area_Labs_StartGame
{
    lock

    applymovement(OBJ_EVENT_ELM, ElmStartGame0)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerStartGame0)
    waitmovement(0)

    msgbox(format("ELM: There you are!\nBack up on your feet now!\pYou seem fine, but perhaps it would be a good idea for you to have a wander around {POKEMON_HUB} to refresh your memory.\pOnce you're ready, come and find me in my lab."))
    closemessage

    applymovement(OBJ_EVENT_ELM, ElmStartGame1)
    waitmovement(0)

    opendoor(7, 6)
    waitdooranim

    applymovement(OBJ_EVENT_ELM, ElmStartGame2)
    waitmovement(0)
    hideobjectat(OBJ_EVENT_ELM, MAP_ROGUE_AREA_LABS)
    setflag(FLAG_HIDE_ELM)

    closedoor(7, 6)
    waitdooranim

    addvar(VAR_ROGUE_INTRO_STATE, 1)
    release
}

movement ElmStartGame0 
{
    walk_in_place_right
}

movement PlayerStartGame0 
{
    face_left
}

movement ElmStartGame1
{
    walk_left
    walk_up * 2
}

movement ElmStartGame2
{
    walk_up
}

script Rogue_Area_Labs_TutorialGuide
{
    lock

    turnobject(OBJ_EVENT_ELM, DIR_NORTH)
    msgbox(format("ELM: {PLAYER}!\pQuickly, we must make haste!"))

    closemessage
    setvar(VAR_0x8004, HUB_AREA_ADVENTURE_ENTRANCE)
    special(Rogue_CheckHubConnectionDir)

    switch(var(VAR_RESULT))
    {
        case HUB_AREA_CONN_NORTH:
            applymovement(OBJ_EVENT_ELM, ElmTutorialGuideNorth)
            break

        case HUB_AREA_CONN_EAST:
            applymovement(OBJ_EVENT_ELM, ElmTutorialGuideEast)
            break

        case HUB_AREA_CONN_SOUTH:
            applymovement(OBJ_EVENT_ELM, ElmTutorialGuideSouth)
            break

        case HUB_AREA_CONN_WEST:
            applymovement(OBJ_EVENT_ELM, ElmTutorialGuideWest)
            break
    }

    waitmovement(0)
    setobjectxy(OBJ_EVENT_ELM, 0, 0)
    setobjectxyperm(OBJ_EVENT_ELM, 0, 0)
    hideobjectat(OBJ_EVENT_ELM, MAP_ROGUE_AREA_LABS)
    setflag(FLAG_HIDE_ELM)

    addvar(VAR_ROGUE_INTRO_STATE, 1)

    release
}

movement ElmTutorialGuideNorth
{
    walk_fast_right * 6
    walk_fast_up * 9
}

movement ElmTutorialGuideEast
{
    walk_fast_right * 9
}

movement ElmTutorialGuideSouth
{
    walk_fast_right * 6
    walk_fast_down * 4
}

movement ElmTutorialGuideWest
{
    walk_fast_left * 7
}

script Rogue_Area_Labs_LabSign
{
    msgbox(format("Professor Elm's Lab"), MSGBOX_SIGN)
}

script Rogue_Area_Labs_ConfigSign
{
    msgbox(format("Config Lab"), MSGBOX_SIGN)
}

script Rogue_Area_Labs_UniqueMonSign
{
    msgbox(format("The sign is worse for wearâ€¦\nOnly a few words can be made out.\p???'s Lab"), MSGBOX_SIGN)
}