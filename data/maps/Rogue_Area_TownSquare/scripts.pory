const FLAG_HIDE_LOCAL_WORKBENCH = FLAG_TEMP_1
const FLAG_HIDE_MOVE_TUTORS = FLAG_TEMP_2
const FLAG_HIDE_BACK_PACKER = FLAG_TEMP_3
const FLAG_HIDE_BALL_GUY = FLAG_TEMP_4

const OBJ_EVENT_BUILDER = 3

mapscripts Rogue_Area_TownSquare_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Area_TownSquare_OnMapLoad
}

script Rogue_Area_TownSquare_OnMapLoad
{
    specialvar(VAR_RESULT, RogueHub_GetStatueLevel)

    switch(var(VAR_RESULT))
    {
        case 3:
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_STATUE_LEVEL_3)
        case 2:
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_STATUE_LEVEL_2)
        case 1:
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_STATUE_LEVEL_1)
        default:
            setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_STATUE_LEVEL_0)
    }


    if(var(VAR_ROGUE_INTRO_STATE) < ROGUE_INTRO_STATE_REPORT_TO_PROF)
    {
        // blocking connect centre
        setflag(FLAG_HIDE_LOCAL_WORKBENCH)

        setobjectxyperm(OBJ_EVENT_BUILDER, 21, 8)
        setobjectmovementtype(OBJ_EVENT_BUILDER, MOVEMENT_TYPE_FACE_UP)

        setflag(FLAG_HIDE_BACK_PACKER)
        setflag(FLAG_HIDE_BALL_GUY)
    }
    else
    {
        // blocking workbench
        clearflag(FLAG_HIDE_LOCAL_WORKBENCH)

        clearflag(FLAG_HIDE_BACK_PACKER)
        clearflag(FLAG_HIDE_BALL_GUY)

        if(var(VAR_ROGUE_INTRO_STATE) <= ROGUE_INTRO_STATE_LEARN_TO_BUILD)
        {
            setflag(FLAG_HIDE_BALL_GUY)

            // blocking workbench
            setobjectxyperm(OBJ_EVENT_BUILDER, 24, 9)
            setobjectmovementtype(OBJ_EVENT_BUILDER, MOVEMENT_TYPE_FACE_UP)
        }
    }

    hubupgradeunlocked(HUB_UPGRADE_TOWN_SQUARE_TUTORS)
    if(var(VAR_RESULT) == YES)
    {
        clearflag(FLAG_HIDE_MOVE_TUTORS)
    }
    else
    {
        setflag(FLAG_HIDE_MOVE_TUTORS)
    }
}

script Rogue_Area_TownSquare_TalkTownStatue
{
    lock

    specialvar(VAR_RESULT, RogueHub_GetStatueLevel)

    switch(var(VAR_RESULT))
    {
        case 3:
            msgbox(format("{POKEMON_HUB}\nThe land of chaotic tales."))
        default:
            msgbox(format("{POKEMON_HUB}\nThe land of chaotic beginnings…"))
    }

    special(Special_ViewWorldMap)
    waitstate

    release
}

script Rogue_Area_TownSquare_TalkBackpacker
{
    lock
    faceplayer

    msgbox(format("Hi there {PLAYER}!"))

    specialvar(VAR_0x8004, Rogue_GetBagCapacityUpgradeLevel)

    if(var(VAR_0x8004) == ITEM_BAG_MAX_CAPACITY_UPGRADE)
    {
        msgbox(format("I hope that bag's working out well for you!"))
        release
    }
    else
    {
        special(Rogue_BufferBagUpgradeCost)

        showmoneybox(0, 0)
        msgbox(format("I can upgrade your bag for ¥{STR_VAR_2}, if you'd like?"), MSGBOX_YESNO)

        if(var(VAR_RESULT) == YES)
        {
            special(Rogue_CheckBagUpgradeCost)

            if(var(VAR_RESULT) == YES)
            {
                special(Rogue_RemoveBagUpgradeCost)
                updatemoneybox

                playse(SE_SHOP)
                waitse
                
                hidemoneybox
                release
                delay(1)

                special(Rogue_IncreaseBagCapacityUpgradeLevel)
            }
            else
            {
                hidemoneybox
                msgbox(format("It doesn't look like you have enough money there bud."))
                release
            }
        }
        else
        {
            hidemoneybox
            msgbox(format("Sure! You're busy!\pI get that!"))
            release
        }
    }
}

script Rogue_Area_TownSquare_TalkBuilder
{
    lock
    faceplayer

    if(var(VAR_ROGUE_INTRO_STATE) < ROGUE_INTRO_STATE_LEARN_TO_BUILD)
    {
        // blocking connect centre or workbench
        msgbox(format("I'm almost finished up here.\nCome back and check later."))

        turnobject(OBJ_EVENT_BUILDER, DIR_NORTH)
    }
    elif(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_LEARN_TO_BUILD)
    {
        // do workbench tutorial
        clearflag(FLAG_ROGUE_HIDE_WORKBENCHES)
        addvar(VAR_ROGUE_INTRO_STATE, 1)
        
        msgbox(format("Ah! {PLAYER}!\nJust the person I was waiting for!\pProfessor Elm told me that you were going on Adventures for {POKEMON_HUB} now.\pWhich means you'll now be in charge of collecting Building Supplies for {POKEMON_HUB} expansion.\pHere, let me show you.\pFirstly, you can have this Town Map.\pYou can use it to look at the current layout of {POKEMON_HUB}."))
        
        giveitem(ITEM_TOWN_MAP)
        waitse

        special(Special_ViewWorldMap)
        waitstate
        
        msgbox(format("That's the current layout of {POKEMON_HUB}.\pYou can build and upgrade areas with Building Supplies you gather from completing Quests.\pHere's some I had left over."))
        giveitem(ITEM_BUILDING_SUPPLIES)
    
        msgbox(format("Now if you interact with Workbenches like this one here, you'll be able to spend the Building Supplies on improving {POKEMON_HUB}.\pGive it a try!"))

        waitse
        closemessage
        
        specialvar(VAR_RESULT, GetPlayerFacingDirection)
        switch(var(VAR_RESULT))
        {
            case DIR_NORTH:
                applymovement(OBJ_EVENT_BUILDER, Rogue_Area_TownSquare_WorkbenchBuilder_North)
                applymovement(OBJ_EVENT_ID_PLAYER, Rogue_Area_TownSquare_WorkbenchPlayer_North)
                waitmovement(0)
            case DIR_EAST:
                applymovement(OBJ_EVENT_BUILDER, Rogue_Area_TownSquare_WorkbenchBuilder_East)
                applymovement(OBJ_EVENT_ID_PLAYER, Rogue_Area_TownSquare_WorkbenchPlayer_East)
                waitmovement(0)
            case DIR_WEST:
                applymovement(OBJ_EVENT_BUILDER, Rogue_Area_TownSquare_WorkbenchBuilder_West)
                applymovement(OBJ_EVENT_ID_PLAYER, Rogue_Area_TownSquare_WorkbenchPlayer_West)
                waitmovement(0)
        }

        setobjectxyperm(OBJ_EVENT_BUILDER, 25, 9)

        special(Rogue_IsMultiplayerClient)
        if(var(VAR_RESULT) == YES)
        {
            msgbox(format("You cannot use the Workbench while visiting {POKEMON_HUB}.\pIf you want to use the Workbench, you need to Exit Multiplayer."))
        }
        else
        {
            call(Rogue_Area_WorkbenchOptions)
        }
    }
    else
    {
        // idle
        msgbox(format("Just walk up to any of the Workbenches and you should be able to queue up build orders for {POKEMON_HUB}."))
    }

    release
}

movement Rogue_Area_TownSquare_WorkbenchPlayer_North
{
    delay_16
    walk_up
    face_up
}

movement Rogue_Area_TownSquare_WorkbenchBuilder_North
{
    walk_right
    face_left
}

movement Rogue_Area_TownSquare_WorkbenchPlayer_East
{
    delay_16
    walk_right
    face_up
}

movement Rogue_Area_TownSquare_WorkbenchBuilder_East
{
    walk_right
    face_left
}

movement Rogue_Area_TownSquare_WorkbenchPlayer_West
{
    delay_16
    walk_left
    face_up
}

movement Rogue_Area_TownSquare_WorkbenchBuilder_West
{
    walk_left
    face_right
}

script Rogue_Area_TownSquare_TalkBallGuy
{
    lock
    faceplayer

    if(var(VAR_ROGUE_FREE_POKE_BALL) == ITEM_NONE)
    {
        msgbox(format("Hey there! Thanks for rolling by to have a chat with your mate- the Ball Guy.\pDon't worry.\nNext time, I'll give you yet another Poké Ball!\pYou ain't stopping, so neither am I!"))
    }
    else
    {
        msgbox(format("Hey there! Thanks for rolling by to have a chat with your mate- the Ball Guy.\pI've got the perfect Poké Ball for you!"))
    
        checkitemspace(VAR_ROGUE_FREE_POKE_BALL, 1)

        if(var(VAR_RESULT) == YES)
        {
            giveitem(VAR_ROGUE_FREE_POKE_BALL, 1)
            setvar(VAR_ROGUE_FREE_POKE_BALL, ITEM_NONE)
        }
        else
        {
            msgbox(format("Oh, bud.\nYou've got no space in your Bag.\pFree up some space and then I'll be happy to give you a Poké Ball!"))
        }
    }

    release
}