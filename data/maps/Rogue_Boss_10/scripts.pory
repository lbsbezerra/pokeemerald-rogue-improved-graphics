const BOSS_NPC = 1

mapscripts Rogue_Boss_10_MapScripts
{
}

script Rogue_Battle_Boss10
{
    lockall()
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToBattleFieldBoss10)
    waitmovement(0)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraToBattleFieldBoss10)
    waitmovement(0)

    buffertrainername(STR_VAR_1, VAR_ROGUE_SPECIAL_ENCOUNTER_DATA)
    special(Rogue_ShouldSkipTrainerOpenningMsg)
    if(var(VAR_RESULT) == NO)
    {
        msgbox(gPlaceholder_Gym_PreBattleOpenning)
    }

    call(Rogue_SetDesiredWeather)

    applymovement(BOSS_NPC, BossToFightBoss10)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerToFightBoss10)
    waitmovement(0)
    
    // Manually kick off the encouter to control the music + avoid the trainer from changing directions
    special(Rogue_PlayStaticTrainerEncounterBGM)
    msgbox(gPlaceholder_Gym_PreBattleTaunt)
    trainerbattle(TRAINER_BATTLE_SINGLE_NO_INTRO_TEXT, VAR_ROGUE_SPECIAL_ENCOUNTER_DATA, 0, gPlaceholder_Gym_PostBattleTaunt)
    call(Rogue_BattleDefeated_Boss10)
}

script Rogue_BattleDefeated_Boss10
{
    call(Rogue_ClearWeather)
    call(Rogue_Common_DisplayNewBadgePopup)

    msgbox(gPlaceholder_Gym_PostBattleCloser)
    waitmessage()
    call(Rogue_EventScript_AttemptPostGymHeal)
    special(RemoveCameraObject)
    warp(MAP_ROGUE_HUB_TRANSITION, 0)
    waitstate
}

movement PlayerToBattleFieldBoss10 {
    walk_up * 10
}

movement CameraToBattleFieldBoss10 {
    walk_up * 3
}

movement PlayerToFightBoss10 {
    walk_up
}

movement BossToFightBoss10 {
    walk_down
}