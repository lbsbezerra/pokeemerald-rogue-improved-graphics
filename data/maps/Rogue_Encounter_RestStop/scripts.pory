
// Randoman always should have ID as 1
const NPC_RANDOM_MAN = 1

mapscripts Rogue_Encounter_RestStop_MapScripts 
{

}

script Rogue_Encounter_RestStop_Heal
{
    lock
    faceplayer
    msgbox(format("You look tired.\nLet me patch you up!"), MSGBOX_DEFAULT)
    call(Rogue_EventScript_AttemptNurseHeal)
    special(Rogue_OnHealWithNurse)
    msgbox(format("Take care!"), MSGBOX_DEFAULT)
    release
}

script Rogue_Encounter_RestStop_RandomMan
{
    lock
    applymovement(VAR_LAST_TALKED, Rogue_RestStop_RandomMan_Spin)
    waitmovement(0)
    faceplayer

    msgbox(format("WAHEY!\pYou looking to add a bit of randomness to your life?\pTell you what…"), MSGBOX_DEFAULT)
    
    showmoneybox(0, 0)
    msgbox(format("For ¥1000 I'll trade you one Pokémon.\nOR!\pI'll trade ALL your Pokémon for free?"), MSGBOX_DEFAULT)

    message(format("What do you say?"))
    waitmessage

    multichoice(17, 6, MULTI_RANDOM_SWAP_OPTIONS, NO)
    hidemoneybox

    switch(var(VAR_RESULT))
    {
        case 0: // Trade 1
            checkmoney(1000)
            if(var(VAR_RESULT) == YES)
            {
                special(ChoosePartyMon)
                waitstate

                if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
                {
                    msgbox(format("Blagh!\nHow boring…"), MSGBOX_DEFAULT)
                }
                else
                {
                    removemoney(1000)
                    updatemoneybox

                    fadescreen(FADE_TO_BLACK)
                    call(Rogue_ScriptHideFollower)
                    playfanfare(MUS_MOVE_DELETED)
                    special(Rogue_RandomisePartyMon)
                    waitfanfare
                    fadescreen(FADE_FROM_BLACK)
                    call(Rogue_ScriptShowFollower)

                    msgbox(format("Ke ke ke!\pEnjoy!"))
                    applymovement(VAR_LAST_TALKED, Rogue_RestStop_RandomMan_RunAway)
                    waitmovement(0)

                    //completequest(QUEST_MrRandoman)
                    //deactivatequest(QUEST_ChaosChampion) // We have to trade entire team for this to be valid
                    setflag(FLAG_ROGUE_RANDOM_TRADE_DISABLED)
                    clearflag(FLAG_ROGUE_RANDOM_TRADE_WAS_FULL_PARTY)
                    removeobject(VAR_LAST_TALKED)
                }
            }
            else
            {
                msgbox(format("Blagh!\nHow boring…\pCome back with some more money."), MSGBOX_DEFAULT)
            }

        case 1: // Trade all
            msgbox(format("Oh boy! So you wanna trade your whole team?"), MSGBOX_YESNO)

            if(var(VAR_RESULT) == YES)
            {
                setvar(VAR_0x8004, 255)

                fadescreen(FADE_TO_BLACK)
                call(Rogue_ScriptHideFollower)
                playfanfare(MUS_MOVE_DELETED)
                special(Rogue_RandomisePartyMon)
                waitfanfare
                fadescreen(FADE_FROM_BLACK)
                call(Rogue_ScriptShowFollower)
                
                msgbox(format("Ke ke ke!\pEnjoy!"))
                applymovement(VAR_LAST_TALKED, Rogue_RestStop_RandomMan_RunAway)
                waitmovement(0)

                //completequest(QUEST_MrRandoman)
                setflag(FLAG_ROGUE_RANDOM_TRADE_DISABLED)
                setflag(FLAG_ROGUE_RANDOM_TRADE_WAS_FULL_PARTY)
                removeobject(VAR_LAST_TALKED)
            }
            else
            {
                msgbox(format("Blagh!\nHow boring…"), MSGBOX_DEFAULT)
            }

        case MULTI_B_PRESSED:
            msgbox(format("Blagh!\nHow boring…"), MSGBOX_DEFAULT)

    }
    release
}

movement Rogue_RestStop_RandomMan_Spin
{
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1
    
    face_right
    delay_1

    face_up
    delay_1

    face_left
    delay_1

    face_down
    delay_1

    jump_in_place_down
}

movement Rogue_RestStop_RandomMan_RunAway
{
    jump_2_right * 4
}