mapscripts Rogue_Interior_Home_MapScripts 
{
    MAP_SCRIPT_ON_RESUME: Rogue_Interior_Home_OnResume
}

script Rogue_Interior_Home_OnResume
{
    setstepcallback(STEP_CB_SECRET_BASE)
}

script Rogue_Interior_Home_TurnOnPC
{
    lockall
    setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
    special(DoPCTurnOnEffect)

    playse(SE_PC_ON)
    msgbox(format("{PLAYER} booted up the PC."), MSGBOX_DEFAULT)
    special(BedroomPC)
    waitstate
    
    // Not sure why this shouldn't usually be done from script??
    // Usually do it from PlayerPC_TurnOff
    setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
    playse(SE_PC_OFF)
    special(DoPCTurnOffEffect)

    releaseall
}

script Rogue_Interior_Home_TurnOnPCLowerFloor
{
    hubupgradeunlocked(HUB_UPGRADE_HOME_UPPER_FLOOR)
    if(var(VAR_RESULT) == NO)
    {
        lockall
        setvar(VAR_0x8004, PC_LOCATION_MAYS_HOUSE)
        special(DoPCTurnOnEffect)

        playse(SE_PC_ON)
        msgbox(format("{PLAYER} booted up the PC."), MSGBOX_DEFAULT)
        special(BedroomPC)
        waitstate
        
        // Not sure why this shouldn't usually be done from script??
        // Usually do it from PlayerPC_TurnOff
        setvar(VAR_0x8004, PC_LOCATION_MAYS_HOUSE)
        playse(SE_PC_OFF)
        special(DoPCTurnOffEffect)

        releaseall
    }
}

script Rogue_Interior_Home_NoteBook
{
    lockall

    msgbox(format("It's a notebook!\pIt has a bunch of incoherent scribblings from…\psomeone called…\pTate?\pWhy's it here?"), MSGBOX_DEFAULT)

    releaseall
}

script Rogue_Interior_Home_ChangeClothes
{
    lockall

    specialvar(VAR_RESULT, RoguePlayer_HandleEasterEggOutfitUnlocks)

    if(var(VAR_RESULT) == YES)
    {
        call(Rogue_Common_DisplayPopups)
    }

    message(format("What would you like to do?"))
    waitmessage

    multichoice(20, 6, MULTI_CHANGE_CLOTHES, NO)
    
    switch(var(VAR_RESULT))
    {
        case 0: // change outfit
            fadescreen(FADE_TO_BLACK)
            special(Special_ViewPlayerCustomisationMenu)
            waitstate

        case 1: // change name
            fadescreen(FADE_TO_BLACK)
            special(ChangePlayerNickname)
            waitstate

        // Fallthrough and exit
    }

    special(Rogue_FixPartyMonDetails)
    releaseall
}

script Rogue_Interior_Home_EnterBed
{
    lockall

    message(format("What time would you like to wake up?"))
    waitmessage

    multichoice(21, 4, MULTI_SELECT_TIME, NO)

    // Copy season selection
    copyvar(VAR_0x8004, VAR_RESULT)
    setvar(VAR_0x8005, MULTI_B_PRESSED)

    if(var(VAR_RESULT) != MULTI_B_PRESSED)
    {
        message(format("Which season would you like to wake up in?"))
        waitmessage

        multichoice(21, 4, MULTI_SELECT_SEASON, NO)

        copyvar(VAR_0x8005, VAR_RESULT)
    }

    special(Rogue_SetTimeAndSeason)

    if(var(VAR_RESULT) == TRUE)
    {
        closemessage

        fadescreen(FADE_TO_BLACK)

        //playfanfare(MUS_RG_POKE_FLUTE)
        //waitfanfare

        fadeoutbgm(1)
        playbgm(MUS_NONE, FALSE)
        delay(30)

        //playse(SE_M_GRASSWHISTLE)
        //waitse
        playse(SE_M_SNORE)
        waitse
        delay(50)

        special(ReloadWarpSilent)

        //fadeinbgm(1)

        //fadescreen(FADE_FROM_BLACK)

        //msgbox("Good morning!")
    }

    //applymovement(OBJ_EVENT_ID_PLAYER, PlayerLeaveBed)
    //waitmovement(0)

    //releaseall
}


movement PlayerLeaveBed
{
    walk_right
}