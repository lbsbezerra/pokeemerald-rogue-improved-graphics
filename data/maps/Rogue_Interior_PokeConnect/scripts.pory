const VAR_STOP_TUTORIAL = VAR_TEMP_0

mapscripts Rogue_Interior_PokeConnect_MapScripts 
{
    MAP_SCRIPT_ON_LOAD: Rogue_Interior_PokeConnect_OnMapLoad
}

script Rogue_Interior_PokeConnect_OnMapLoad
{
    if(flag(FLAG_ROGUE_ASSISTANT_TUTORIAL))
    {
        setvar(VAR_STOP_TUTORIAL, 1)
    }
    else
    {
        setvar(VAR_STOP_TUTORIAL, 0)
    }
}

script Rogue_Interior_PokeConnect_TalkMultiplayer
{
    lock

    special(Rogue_IsMultiplayerActive)
    if(var(VAR_RESULT) == TRUE)
    {
        msgbox(format("Hello {PLAYER}!\pI hope you've been enjoying your Multiplayer Adventure!"), MSGBOX_DEFAULT)
    
        special(Rogue_IsMultiplayerHost)
        if(var(VAR_RESULT) == TRUE)
        {
            msgbox(format("Would you like to stop Hosting?"), MSGBOX_YESNO)
        }
        else
        {
            msgbox(format("Would you like to Disconnect and return to home?"), MSGBOX_YESNO)
        }

        if(var(VAR_RESULT) == TRUE)
        {
            special(Rogue_CloseMultiplayer)
            msgbox(format("There you are!\pI hope you enjoyed your Multiplayer Adventures!"), MSGBOX_DEFAULT)
        }
    }
    else
    {
        msgbox(format("Hello {PLAYER}!\pI can help you to setup Multiplayer Adventures for you and a friend!"), MSGBOX_DEFAULT)

        msgbox(format("{COLOR RED}WARNING!\n{COLOR DARK_GRAY}This feature is still in Alpha.\pIt is still in active development, as a result, some features may not work correctly/be missing.\pPlease regularly save your game and make save backups, while using this feature."))

        call(Rogue_Common_WaitForRogueAssistant)

        if(var(VAR_RESULT) == TRUE)
        {
            call(Common_EventScript_SaveGame)
            if(var(VAR_RESULT) == YES)
            {
                message(format("Please decide which of you will become the Group Host.\pThe other players must then choose Join Group."))
                waitmessage

                multichoice(16, 6, MULTI_LINK_HOST_JOIN, FALSE)

                switch(var(VAR_RESULT))
                {
                    case 0: // Join
                        message(format("Please setup in Rogue Assistant now\n… … B Button: Cancel"))
                        waitmessage

                        special(Rogue_JoinMultiplayer)
                        waitstate

                        if(var(VAR_RESULT) == TRUE)
                        {
                            msgbox(format("All set!\nHave fun on your Adventures!"))
                        }

                    case 1: // Host
                        message(format("Please setup in Rogue Assistant now\n… … B Button: Cancel"))
                        waitmessage

                        special(Rogue_HostMultiplayer)
                        waitstate

                        if(var(VAR_RESULT) == TRUE)
                        {
                            msgbox(format("All set!\nA friend should now be able to join you for Adventures!"))
                        }

                    case 2: // Exit
                        setvar(VAR_RESULT, FALSE)

                }
            }
        }

        if(var(VAR_RESULT) == FALSE)
        {
            msgbox(format("If you change your mind, please feel free to speak with me again."))
        }
    }

    release
}

script Rogue_Interior_PokeConnect_TalkNurseBox
{
    lock

    msgbox(format("Hello {PLAYER}!"))

    setvar(VAR_RESULT, 1)

    while(var(VAR_RESULT) == 1)
    {
        message(format("Would you like to access the Extended Pokémon Storage?"))
        waitmessage

        multichoice(20, 6, MULTI_YESNOINFO, NO)

        switch(var(VAR_RESULT))
        {
            case 0: // Yes
                call(Rogue_Interior_PokeConnect_ConnectHomeBox)
                setvar(VAR_RESULT, 0)

            case 1: // No
            case MULTI_B_PRESSED:
                setvar(VAR_RESULT, 0)

            case 2: // Info
                msgbox(format("By connecting to RogueAssistant, you may access an extended Pokémon Storage similar to Pokémon Home, allowing you to store many more Pokémon!"), MSGBOX_DEFAULT)
                setvar(VAR_RESULT, 1)
        }
    }

    msgbox(format("Take care!"))
    release
}

script Rogue_Interior_PokeConnect_ConnectHomeBox
{
    call(Rogue_Common_WaitForRogueAssistant)
    
    if(var(VAR_RESULT) == TRUE)
    {
        call(Common_EventScript_SaveGame)
        if(var(VAR_RESULT) == YES)
        {
            special(Special_ViewAssistantBox)
            waitstate

            call(Common_EventScript_ForceSaveGame)
        }
    }
}

script Rogue_Interior_PokeConnect_Tutorial
{
    setflag(FLAG_ROGUE_ASSISTANT_TUTORIAL)
    setvar(VAR_STOP_TUTORIAL, 1)

    lock
    msgbox(format("This building grants access to external features such as Extended Storage and Multiplayer.\pCurrently, these features are only supported when playing using mGBA on Windows PC.\pIn order to make use of these features, you will need to download a helper program called Rogue Assistant and connect it to your emulator.\pDownloaded from:\n{COLOR BLUE}https://rogue.assist.pokabbie.com\p{COLOR RED}Never download from other links, otherwise you may open yourself up to installing viruses!"))
    release
}