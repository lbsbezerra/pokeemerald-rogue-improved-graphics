const FLAG_HIDE_BIRCH = FLAG_TEMP_1
const FLAG_HIDE_RUIN_MANIAC = FLAG_TEMP_2

const VAR_FRAME_STATE = VAR_TEMP_0

const OBJ_EVENT_BIRCH = 1
const OBJ_EVENT_ASSISTANT = 2
const OBJ_EVENT_RUIN_MANIAC = 3

mapscripts Rogue_Interior_ProfLab_MapScripts
{
    MAP_SCRIPT_ON_LOAD: Rogue_Interior_ProfLab_OnMapLoad
    MAP_SCRIPT_ON_FRAME_TABLE 
    [
        VAR_FRAME_STATE, 0: Rogue_Interior_ProfLab_FrameState0
    ]
}

script Rogue_Interior_ProfLab_OnMapLoad
{
    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_EXPLORE)
    {
        clearflag(FLAG_HIDE_RUIN_MANIAC)
    }
    else
    {
        setflag(FLAG_HIDE_RUIN_MANIAC)
    }

    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_CATCH_MON)
    {
        setflag(FLAG_HIDE_BIRCH)
    }
    else
    {
        clearflag(FLAG_HIDE_BIRCH)
    }

    if(var(VAR_ROGUE_INTRO_STATE) == ROGUE_INTRO_STATE_REPORT_TO_PROF)
    {
        setvar(VAR_FRAME_STATE, 0)
    }
    else
    {
        setvar(VAR_FRAME_STATE, 1)
    }
}

script Rogue_Interior_ProfLab_FrameState0
{
    lock

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)

    msgbox(format("ELM: There we are!\pI must say I was pretty impressed by your catching technique!\pI can't think of a better Trainer to have going on Adventures for {POKEMON_HUB}!"))
    msgbox(format("Now, {PLAYER}.\nDid you want me to explain how Adventures work?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        call(Rogue_Interior_ProfLab_TalkBirch_AdventureExplain)
    }
    else
    {
        msgbox(format("Your memory is better than I expected!\nFuhahaha!"))
    }

    msgbox(format("Before you go, you should take these!"))
    giveitem(ITEM_POKE_BALL, 10)
    giveitem(ITEM_POTION, 5)

    msgbox(format("And finally, your very own Pokédex!"))
    
    if(flag(FLAG_SYS_POKEDEX_GET))
    {
        msgbox(format("Oh, it looks like you already got a Pokédex from Pokabbie!\pI guess you don't need this one then!\nFuhahaha!"))
    }
    else
    {
        callnative(Rogue_PushPopup_UnlockPokedex)
        call(Rogue_Common_DisplayPopups)
        setflag(FLAG_SYS_POKEDEX_GET)
    }
    setflag(FLAG_SYS_POKEMON_GET)

    msgbox(format("That Pokédex has been recently upgraded too!\pIt can show you Pokemon's stats, moves, evolutions and more!"))
    msgbox(format("You should be all set to go now!\pGood luck, {PLAYER}!"))
    closemessage

    addvar(VAR_ROGUE_INTRO_STATE, 1)
    addvar(VAR_FRAME_STATE, 1)

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ASSISTANT, IntroAssistant0)
    waitmovement(0)

    setvar(VAR_0x8004, 0)
    special(BufferMonNickname)
    msgbox(format("Hi {PLAYER}!\pYour {STR_VAR_1} seems to really enjoy walking around outside of it's Poké Ball.\pWould you like {STR_VAR_1} to follow you?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        setflag(FLAG_SYS_SHOW_POKE_FOLLOWER)
        special(Rogue_SetupFollowParterMonObjectEvent)

        msgbox(format("{STR_VAR_1} looks so happy!"))
    }
    else
    {
        clearflag(FLAG_SYS_SHOW_POKE_FOLLOWER)
        special(Rogue_SetupFollowParterMonObjectEvent)

        msgbox(format("Oh…\nIf you change your mind, just let me know."))
    }
    closemessage

    applymovement(OBJ_EVENT_ASSISTANT, IntroAssistant1)
    waitmovement(0)


    release
}

movement IntroAssistant0
{
    walk_left * 4
    walk_up * 4
}

movement IntroAssistant1
{
    walk_right
    face_left
}

script Rogue_Interior_ProfLab_TalkBirch
{
    lock
    faceplayer
    msgbox(format("ELM: Hi {PLAYER}!\pDid you want me to explain how Adventures work?"), MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES)
    {
        call(Rogue_Interior_ProfLab_TalkBirch_AdventureExplain)
    }


    release
}

script Rogue_Interior_ProfLab_TalkAssistant
{
    lock
    faceplayer

    if(var(VAR_ROGUE_INTRO_STATE) < ROGUE_INTRO_STATE_REPORT_TO_PROF)
    {
        msgbox(format("Hi {PLAYER}!\pI hope you're doing well!\nI heard you hit your head."))
    }
    else
    {
        setvar(VAR_0x8004, 0)
        special(BufferMonNickname)
        if(flag(FLAG_SYS_SHOW_POKE_FOLLOWER))
        {
            msgbox(format("Hi {PLAYER}!\pWould you like to put {STR_VAR_1} back in it's Poké Ball?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES)
            {
                clearflag(FLAG_SYS_SHOW_POKE_FOLLOWER)
            }
        }
        else
        {
            msgbox(format("Hi {PLAYER}!\pWould you like {STR_VAR_1} to follow you?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES)
            {
                setflag(FLAG_SYS_SHOW_POKE_FOLLOWER)
            }
        }
        special(Rogue_SetupFollowParterMonObjectEvent)
    }

    release
}

script Rogue_Interior_ProfLab_TalkBirch_AdventureExplain
{
    msgbox(format("As you may remember, the world outside of {POKEMON_HUB} has gone a bit strange.\pRoutes will be shuffled around and may appear in a new random order every time you embark on an Adventure."))
    msgbox(format("When you leave on Adventures you'll notice that your Pokémon's level will get reset.\pIt will still know all of the same moves however."))
    msgbox(format("The items in your bag also possess a strange quality.\pWhenever you leave on Adventure the state of your Bag seems to be remembered!\pWhen you finally return to {POKEMON_HUB}, your bag will be in the very same state as it was when you left.\pLet's say, for example, you find a Super Potion whilst you're Adventuring.\pWhen you return, that Super Potion will magically have disappeared from your Bag!\pThe opposite is also true. For example, if you have a Poké Ball in your Bag when you leave and you use it on your Adventure, when you return, you'll notice that it will have reappered right back inside your Bag!"))
    msgbox(format("Rather unsurprisingly, Trainers are still very much itching for fights too!\pSo, be careful and make sure to train your Pokémon well!"))
    msgbox(format("It's all very strange!\pWe're still trying to research it and figure out what's going on!"))
}

script Rogue_Interior_ProfLab_TriggerIntro0
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerIntro0)
    waitmovement(0)
    call(Rogue_Interior_ProfLab_TriggerIntroInternal)
    release
}

script Rogue_Interior_ProfLab_TriggerIntro1
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerIntro1)
    waitmovement(0)
    call(Rogue_Interior_ProfLab_TriggerIntroInternal)
    release
}

script Rogue_Interior_ProfLab_TriggerIntro2
{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerIntro2)
    waitmovement(0)
    call(Rogue_Interior_ProfLab_TriggerIntroInternal)
    release
}

script Rogue_Interior_ProfLab_TriggerIntroInternal
{
    msgbox(format("ELM: Hi {PLAYER}!\pIt looks like you've refreshed your memory a bit now!\pNow that {POKEMON_HUB} is properly established, we need someone to go out on Adventures to explore and gather resources.\pIf we can do that, we'll be able to build up and expand the village.\pThat's where you come in {PLAYER}!\pIn order for you to do that you'll need a Pokémon Partner…"))

    playbgm(MUS_NONE, FALSE)
    msgbox(format("PROFFFFFFESSOR!"))
    closemessage

    setobjectxy(OBJ_EVENT_RUIN_MANIAC, 4, 11)
    //showobjectat(OBJ_EVENT_RUIN_MANIAC, MAP_ROGUE_INTERIOR_PROF_LAB)
    playbgm(MUS_HELP, FALSE)
    
    applymovement(OBJ_EVENT_RUIN_MANIAC, RuinManiacIntro0)
    waitmovement(0)

    msgbox(format("P- P- Pokémon are invading!"))

    applymovement(OBJ_EVENT_BIRCH, BirchIntro0)
    waitmovement(0)

    msgbox(format("ELM: What do you mean!?\pThis can't be good {PLAYER}.\pWe better go help!"))
    closemessage

    applymovement(OBJ_EVENT_RUIN_MANIAC, RuinManiacIntro1)
    applymovement(OBJ_EVENT_BIRCH, BirchIntro1)
    waitmovement(0)

    hideobjectat(OBJ_EVENT_RUIN_MANIAC, MAP_ROGUE_INTERIOR_PROF_LAB)
    hideobjectat(OBJ_EVENT_BIRCH, MAP_ROGUE_INTERIOR_PROF_LAB)
    setflag(FLAG_HIDE_RUIN_MANIAC)
    setflag(FLAG_HIDE_BIRCH)

    addvar(VAR_ROGUE_INTRO_STATE, 1)
}

movement PlayerIntro0
{
    walk_right
    walk_up * 2
}

movement PlayerIntro1
{
    walk_up * 2
}

movement PlayerIntro2
{
    walk_left
    walk_up * 2
}

movement RuinManiacIntro0
{
    walk_fast_up * 6
}

movement RuinManiacIntro1
{
    walk_fast_down * 10
}

movement BirchIntro0
{
    emote_exclamation_mark
}

movement BirchIntro1
{
    walk_fast_right
    walk_fast_down * 10
}