const VAR_CURR_HUB_UPGRADE = VAR_0x8004 // NOT TO BE USED AT SAME TIME *[0]
const VAR_CURR_HUB_AREA = VAR_0x8004 // NOT TO BE USED AT SAME TIME *[0]
const VAR_CURR_DIRECTION = VAR_0x8005

script Rogue_Area_InteractWithWorkbench
{
    special(Rogue_IsMultiplayerClient)
    if(var(VAR_RESULT) == YES)
    {
        lock
        msgbox(format("It's a workbench!\pIt's has a bunch of complicated designs and tools on top."))
        release
    }
    elif(var(VAR_ROGUE_INTRO_STATE) > ROGUE_INTRO_STATE_LEARN_TO_BUILD)
    {
        specialvar(VAR_RESULT, GetPlayerFacingDirection)
        if(var(VAR_RESULT) != DIR_SOUTH) // Can't interact from the top
        {
            lock
            call(Rogue_Area_WorkbenchOptions)
            release
        }
    }
    else
    {
        lock
        msgbox(format("It's a workbench!\pIt's has a bunch of complicated designs and tools on top."))
        release
    }
}

script Rogue_Area_WorkbenchOptions
{
    lock
    message("What would you like to build?")
    waitmessage

    rogue_dynamicpokemart(ROGUE_SHOP_HUB_UPGRADES)

    // Play build animation
    if(var(VAR_RESULT) == TRUE)
    {
        fadescreen(FADE_TO_BLACK)
        
        call(Rogue_Area_PlayBuildSound)

        msgbox(format("{POKEMON_HUB} has been upgraded!"), MSGBOX_DEFAULT)

        waitfanfare
        special(ReloadWarpSilent)
    }

    release
}

// Utils
//

script Rogue_Area_PlayBuildSound
{
    closemessage
    playbgm(MUS_NONE, FALSE)
    delay(15)

    playse(SE_BRIDGE_WALK)
    waitse
    playse(SE_BRIDGE_WALK)
    waitse
    playse(SE_BRIDGE_WALK)
    waitse
    delay(15)

    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    delay(15)

    playse(SE_M_VICEGRIP)
    waitse
    delay(15)

    playfanfare(MUS_LEVEL_UP)
}